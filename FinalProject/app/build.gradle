apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationIdSuffix ".free"
        }
        paid {
            applicationIdSuffix ".paid"
        }
    }
}

configurations.all() {
    exclude group: "org.apache.httpcomponents", module: "httpclient"
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile project(":javajokelibrary")
    compile project(":javajokeandroidlibrary")
    compile group: 'com.google.api-client', name: 'google-api-client', version: '1.22.0'
    compile group: 'com.google.api-client', name: 'google-api-client-android', version: '1.22.0'

    //Testing-only dependencies
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'

    //Free Version dependencies
    freeCompile 'com.google.android.gms:play-services-ads:11.0.4'
}

task testForNonEmptyStringsText {
    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }

    mustRunAfter ':backend:appengineRun'
    dependsOn = [':backend:appengineRun', ':app:connectedAndroidTest']
    finalizedBy ':backend:appengineStop'
}







